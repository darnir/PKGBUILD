# Maintainer: Darshit Shah <darnir@gmail.com>

_gitname=wget
pkgname=$_gitname-git
pkgver=v1.16.85.g09d47ea
pkgrel=1
pkgdesc="A network utility to retrieve files from the Web"
arch=('i686' 'x86_64')
url="http://www.gnu.org/software/wget/wget.html"
license=('GPL3')
depends=('openssl' 'libidn' 'pcre' 'util-linux' 'libpsl>=0.6.2')
optdepends=("ca-certificates: HTTPS Downloads", "libmetalink: Metalink support.")
makedepends=('git' 'perl')
provides=('wget')
conflicts=('wget')
backup=('etc/wgetrc')
install=wget.install
source=("git://git.savannah.gnu.org/${_gitname}.git"
        "git://git.savannah.gnu.org/gnulib.git"
        "0001-Assertions-are-now-disabled-by-default.patch")
        md5sums=('SKIP' 'SKIP' 'e4fed2fce1658b2dfc7d06ac4378d67a')

build() {
  cd $_gitname
  git submodule init
  git config submodule.gnulib.url "$srcdir/gnulib"
  ./bootstrap
  ./configure --prefix=/usr --without-included-regex --enable-nls --enable-threads=posix --enable-dependency-tracking --with-ssl=openssl --sysconfdir=/etc
  make
}

prepare() {
  cd $_gitname
  cat >> doc/sample.wgetrc <<EOF

# default root certs location
ca_certificate=/etc/ssl/certs/ca-certificates.crt
EOF
git am ../0001-Assertions-are-now-disabled-by-default.patch
}

package() {
  cd $_gitname
  make DESTDIR="$pkgdir/" install
}

pkgver() {
  cd $_gitname
  #Use the tag from the last commit.
  git describe --always | sed 's|-|.|g'
}

# vim:set ts=2 sw=2 tw=0 et:
